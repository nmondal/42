/* Entity End Point Handling */
ENTITY_CONFIG = _shared["ENTITIES"]

def all_versions(){
    begin = int ( req.queryParams("s") , 0 )
    max = int ( req.queryParams("m") , 30 )
    _log.info("urn : {}, begin:{}, max show:{}", entity_urn, begin, max )
    versions = _ds.storage.listVersions( bucket, entity_id, begin, max )
    panic( empty(versions), jstr( {"success" : false, "message" : "such a data version does not exist!" } ), 404 )
    jstr( { "urn" : entity_urn , "versions" : versions } )
}

def at_version(){
    ver_id = req.params("ver")
    old_string = _ds.storage.stringAtVersion( bucket, entity_id, ver_id ) // loads the string version of the data
    panic( empty(old_string), jstr( {"success" : false, "message" : "such a data version does not exist!" } ), 404 )
    old_string  // return the data
}

entity_urn = req.params("id")
// in case it is of the form "aaa:xxx-bbb-yyy:zzz" it is relationship
if ( "-" @ entity_urn ){
    // this is relationships
    #(bucket, entity_id) = [ "relations", entity_urn ]
} else {
    // this is proper entity
    #(entity_class, entity_id) = entity_urn.split(":")
    assert( entity_class @ ENTITY_CONFIG, jstr( {"success" : false, "message" : "entity-class is not configured!" } ), 404 )
    bucket = ENTITY_CONFIG[entity_class]["bucket"]
}
// now process the stuff
op_type = ( req.pathInfo #^ "/version/" )
op_type ? at_version() : all_versions()
